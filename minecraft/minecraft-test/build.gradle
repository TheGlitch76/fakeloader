apply plugin: "org.spongepowered.gradle.vanilla"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17


minecraft {
	version("1.20") // or: latestRelease() or latestSnapshot()
	runs {
		server()
		client() {
			mainClass = "org.quiltmc.loader.impl.launch.knot.KnotClient"
    		classpath.from(project(':').sourceSets.main.output)
    		classpath.from(project(':minecraft').sourceSets.main.output)
			jvmArgs("-Dloader.development=true", "-Dloader.debug.filesystem.dump_contents=true")

			var prefix = this.project.buildDir.toPath()
			jvmArgs("-Dloader.classPathGroups=" + prefix.resolve("classes/java/main") + ";" + prefix.resolve("resources/main"))
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
	implementation project(":")
	// so that it works on runtime
	implementation "org.quiltmc.chasm:chassembly:${project.quilt_chasm}"
	implementation "org.quiltmc.chasm:chasm:${project.quilt_chasm}"

	// Required for mixin annotation processor
	annotationProcessor "org.ow2.asm:asm:${project.asm}"
	annotationProcessor "org.ow2.asm:asm-analysis:${project.asm}"
	annotationProcessor "org.ow2.asm:asm-commons:${project.asm}"
	annotationProcessor "org.ow2.asm:asm-tree:${project.asm}"
	annotationProcessor "org.ow2.asm:asm-util:${project.asm}"

	/**
	 * Ensure we are using the mixin version loader is built against to test the AP.
	 * Otherwise Loom will default to an older version (due to no mod loader on the mod* configs)
	 */
	annotationProcessor ("net.fabricmc:sponge-mixin:${project.sponge_mixin}") {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
	}

	testImplementation project(":junit")
}

test {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release.set(17)
}

//runClient {
//    classpath += project(':').sourceSets.main.output
//    classpath += project(':minecraft').sourceSets.main.output
//}
